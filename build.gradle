plugins {
    id 'fabric-loom' version '0.5-SNAPSHOT'
    id 'maven-publish'
}

apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = "${project.minecraft_version}-${project.mod_build_num}"
group = project.maven_group

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    implementation 'com.google.code.gson:gson:2.8.6'
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

artifacts {
    archives javadocJar, sourcesJar
}

// Note that the ossrhUsername and ossrhPassword, as well as the
// signing.keyId, signing.password, and signing.secretKeyRingFile variables are defined separately
// in my global gradle.properties for protection
signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name "YUNG's API"
                packaging 'jar'
                // optionally artifactId can be defined here
                description "A common API for YUNG's Minecraft mods"
                url 'https://github.com/yungnickyoung/YUNGs-Api'

                scm {
                    url 'https://github.com/yungnickyoung/YUNGs-Api'
                    connection 'scm:git:git://github.com/yungnickyoung/YUNGs-Api.git'
                    developerConnection 'scm:git:ssh://git@github.com:yungnickyoung/YUNGs-Api.git'
                }
                licenses {
                    license {
                        name 'GNU GPLv3'
                        url 'https://github.com/yungnickyoung/YUNGs-Api/blob/fabric-1.16/LICENSE'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        name 'YUNGNICKYOUNG'
                        email 'yungnickyoung@gmail.com'
                    }
                }
            }
        }
    }
}